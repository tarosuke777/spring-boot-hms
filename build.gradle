plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.11'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.owasp.dependencycheck' version '12.1.9'
	id 'jacoco'
}

group = 'com.tarosuke777.hms'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

def versions = [
    springAi: "1.0.1",
	springCloudAws: "3.4.0",
	mapstruct: "1.6.3",
    lombokMapstructBinding: "0.2.0",
	bootstrap: "5.3.3",
    jquery: "3.7.1",
    jqueryUi: "1.14.1"
]

dependencyManagement {
	imports {
		mavenBom "org.springframework.ai:spring-ai-bom:${versions.springAi}"
		mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${versions.springCloudAws}"
	}
}

dependencies {
	// --- Spring Boot Starters ---
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// --- AI & Cloud ---
	implementation 'org.springframework.ai:spring-ai-starter-mcp-server-webmvc'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

	// --- Template Engine & UI ---
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    implementation 'org.webjars:webjars-locator-core'
	implementation "org.webjars:bootstrap:${versions.bootstrap}"
    implementation "org.webjars:jquery:${versions.jquery}"
    implementation "org.webjars:jquery-ui:${versions.jqueryUi}"

	// --- Persistence & Migration ---
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'

	// --- Utilities ---
	implementation "org.mapstruct:mapstruct:${versions.mapstruct}"

	// --- Lombok & Annotation Processors ---
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	annotationProcessor "org.mapstruct:mapstruct-processor:${versions.mapstruct}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${versions.lombokMapstructBinding}"

	testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${versions.mapstruct}"
	testAnnotationProcessor "org.projectlombok:lombok-mapstruct-binding:${versions.lombokMapstructBinding}"

	// --- Testing ---
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

dependencyCheck {
	failBuildOnCVSS = 7.0 // HIGH (高)の脆弱性のみビルドを失敗させる
	suppressionFile = 'config/owasp/suppressions.xml' // 誤検知除外用ファイル
}

tasks.named('jar') {
    enabled = false // 通常のJAR（-plain.jar）(ライブラリ用)は作らない
}

tasks.named('test') {
	useJUnitPlatform()
	systemProperty 'spring.profiles.active', 'test'
	finalizedBy 'jacocoTestReport'
}

tasks.named('jacocoTestReport') {
	dependsOn tasks.named('test')

	reports {
        html.required = true
        xml.required = false
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += [
        "-Amapstruct.unmappedTargetPolicy=ERROR"
    ]
}